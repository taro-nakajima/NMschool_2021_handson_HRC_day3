
# ============= user variables ===============================

# Minimum and maximum energies for integration
IntegE_min = 20.0
IntegE_max = 22.0

# Minimum and Maximum of L index for plotting
L_min=0.4
L_max=1.6

# Minimum and Maximum of intensity for plotting
# Int_min=0
# Int_max=5e5

# ========= please do not change following commands =============

datafile="CsVCl3_processed_data_Ei_102meV.txt"

BinNum_Q=122
BinNum_E=100
Emin=0.5
Estep=1

array Int_ave[BinNum_Q]
array Err_ave[BinNum_Q]
array E_ave[BinNum_Q]
do for [i=1:BinNum_Q]{
    Int_ave[i]=0.0
    Err_ave[i]=0.0
    E_ave[i]=0.0
}

stats datafile using 1 nooutput
N = STATS_records   # number of raws 

array X[N]
array Y[N]
array Int[N]
array Err[N]

# read data from file
stats datafile using (X[$0+1] = $1, Y[$0+1] = $2,Int[$0+1] = $3,Err[$0+1] = $4) nooutput

set print sprintf('constE_%4.1fto%4.1fmeV.txt',IntegE_min,IntegE_max)
print "#L(r.l.u.) E(meV) Int(arb.units) Err"

ave_start=int((IntegE_min-Emin)/Estep)+1
ave_end=int((IntegE_max-Emin)/Estep)+1
ave_span = ave_end-ave_start+1

do for [ave=ave_start:ave_end]{
    do for [i=1:BinNum_Q]{
        Int_ave[i]=Int_ave[i]+Int[ave*BinNum_Q+i]/ave_span
        Err_ave[i]=Err_ave[i]+(Err[ave*BinNum_Q+i]/ave_span)**2.0
        E_ave[i]=E_ave[i]+Y[ave*BinNum_Q+i]/ave_span
    }
}

do for [i=1:BinNum_Q]{
    print sprintf('%8.5f  %8.5f  %8.5f  %8.5f',X[i],E_ave[i],Int_ave[i],sqrt(Err_ave[i]))
}

set out
set print

set term png size 800, 500
set size ratio 0.5  # ratio r = height/width
set out sprintf('constE_%4.1fto%4.1fmeV.png',IntegE_min,IntegE_max)
set key left top

set title sprintf('Const-E cut, %4.1f meV < E < %4.1f meV',IntegE_min,IntegE_max) font "Arial,14" 
set xlabel 'L (r. l. u)' font "Arial,16" 
set ylabel 'Intensity (arb. units)' font "Arial,16" 


set style line 1 lt 1 lc "#ff0000" lw 1 pt 7 ps 1.5
set style line 2 lt 1 lc "#8080FF" lw 2 pt 7 ps 1.5
set style line 3 lt 1 lc "#00C000" lw 1 pt 7 ps 1.5
set style line 11 lt 1 lc "#000000" lw 1 pt 6 ps 1.5

set yrange[0:]
set xrange[L_min:L_max]

plot Int_ave using (X[$1]):2:(sqrt(Err_ave[$1])) w yer ls 1 not,\
Int_ave using (X[$1]):2 w p ls 11 not,\

set auto x
set out


