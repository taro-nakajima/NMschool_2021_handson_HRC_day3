
# ============= user variables ===============================

# text datafile of the const-E profile.
datafile="fitting_result.txt"

# S value for CsVCl3
S=1.5

# Minimum and Maximum of q for plotting
q_min=0.0
q_max=0.5

# ========= please do not change following commands =============

F(x)=4.0*S*J*sin(pi*x)
fit [q_min:q_max] F(x) datafile u 2:1 via J

set term png size 800, 500
set size ratio 0.7  # ratio r = height/width
set out sprintf('dispersion_fit.png',datafile)
set key left top

set title "dispersion relation" font "Arial,14" 
set xlabel 'q (r. l. u)' font "Arial,16" 
set ylabel 'Energy (meV)' font "Arial,16" 


set style line 1 lt 1 lc "#ff0000" lw 1 pt 7 ps 1.5
set style line 2 lt 1 lc "#8080FF" lw 2 pt 7 ps 1.5
set style line 3 lt 1 lc "#00C000" lw 1 pt 7 ps 1.5
set style line 11 lt 1 lc "#000000" lw 1 pt 6 ps 1.5

set yrange[0:]
set xrange[q_min:q_max]

plot F(x) not, datafile u 2:1:3 w xer ls 1 not,\
datafile using 2:1 w p ls 11 not

set out


